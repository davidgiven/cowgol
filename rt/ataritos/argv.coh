var argv_pointer: [uint8];

sub ArgvInit() is
	@asm "move.l (basepage), d0";
	@asm "add.l #129, d0";
	@asm "move.l d0, (", argv_pointer, ")";

	var len := [argv_pointer-1];
	[argv_pointer+(len as intptr)] := 0;
end sub;

# Returns null is there's no next argument.
sub ArgvNext(): (arg: [uint8]) is
	# No more arguments?

	if argv_pointer == nil then
		arg := argv_pointer;
		return;
	end if;

	# Skip leading whitespace.

	var c: uint8;
	loop
		c := [argv_pointer];
		if c != ' ' then
			break;
		end if;
		argv_pointer := argv_pointer + 1;
	end loop;

	arg := argv_pointer;

	# Skip to end of word and terminate.

	loop
		c := [argv_pointer];
		if (c == ' ') or (c == '\n') or (c == 0) then
			break;
		end if;
		argv_pointer := argv_pointer + 1;
	end loop;
	[argv_pointer] := 0;

	if c == ' ' then
		argv_pointer := argv_pointer + 1;
	else
		argv_pointer := 0 as [uint8];
	end if;

	if [arg] == 0 then
		arg := 0 as [uint8];
	end if;
end sub;



