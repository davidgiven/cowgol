const CACHE_SIZE := REGISTER_COUNT;

const CACHE_SLOT_EMPTY    := 0;
const CACHE_SLOT_CONSTANT := 1;
const CACHE_SLOT_ADDRESS  := 2;
const CACHE_SLOT_VALUE    := 3;

record CacheSlot
	symbol: [Symbol];
	number: Word;
	reg: RegId;
	state: uint8;
end record;

var register_cache: CacheSlot[CACHE_SIZE];

sub RegCacheReset()
	MemZero(&register_cache[0] as [uint8], @bytesof register_cache);
end sub;

sub RegCacheFlush(reg: RegId)
	var p := &register_cache[0];
	while p != &register_cache[@sizeof register_cache] loop
		if (p.state != CACHE_SLOT_EMPTY) and ((reg & p.reg) != 0) then
			p.state := CACHE_SLOT_EMPTY;
		end if;
		p := @next p;
	end loop;
end sub;

sub reg_i_find_empty_slot(): (slot: [CacheSlot])
	slot := &register_cache[0];
	while slot != &register_cache[@sizeof register_cache] loop
		if (slot.state == CACHE_SLOT_EMPTY) then
			return;
		end if;
		slot := @next slot;
	end loop;
	slot := 0 as [CacheSlot];
end sub;

sub RegCacheLeavesConstant(reg: RegId, value: Word)
	var p := &register_cache[0];
	while p != &register_cache[@sizeof register_cache] loop
		if (p.state == CACHE_SLOT_CONSTANT) and (p.number == value) then
			p.reg := p.reg | reg;
			return;
		end if;
		p := @next p;
	end loop;

	p := reg_i_find_empty_slot();
	if p != (0 as [CacheSlot]) then
		p.state := CACHE_SLOT_CONSTANT;
		p.number := value;
		p.reg := reg;
	end if;
end sub;

sub RegCacheLeavesAddress(reg: RegId, sym: [Symbol], off: Size)
	var p := &register_cache[0];
	while p != &register_cache[@sizeof register_cache] loop
		if (p.state == CACHE_SLOT_ADDRESS) and (p.symbol == sym) and (p.number == (off as Word)) then
			p.reg := p.reg | reg;
			return;
		end if;
		p := @next p;
	end loop;

	p := reg_i_find_empty_slot();
	if p != (0 as [CacheSlot]) then
		p.state := CACHE_SLOT_ADDRESS;
		p.symbol := sym;
		p.number := off as Word;
		p.reg := reg;
	end if;
end sub;

sub RegCacheLeavesValue(reg: RegId, sym: [Symbol], off: Size)
	var p := &register_cache[0];
	while p != &register_cache[@sizeof register_cache] loop
		if (p.state == CACHE_SLOT_VALUE) and (p.symbol == sym) and (p.number == (off as Word)) then
			p.reg := p.reg | reg;
			return;
		end if;
		p := @next p;
	end loop;

	p := reg_i_find_empty_slot();
	if p != (0 as [CacheSlot]) then
			p.state := CACHE_SLOT_VALUE;
			p.symbol := sym;
			p.number := off as Word;
			p.reg := reg;
	end if;
end sub;

sub RegCacheFindConstant(value: Word): (reg: RegId)
	reg := 0;
	var p := &register_cache[0];
	while p != &register_cache[@sizeof register_cache] loop
		if (p.state == CACHE_SLOT_CONSTANT) and (p.number == value) then
			reg := p.reg;
			return;
		end if;
		p := @next p;
	end loop;
end sub;

sub RegCacheFindAddress(sym: [Symbol], off: Size): (reg: RegId)
	reg := 0;
	var p := &register_cache[0];
	while p != &register_cache[@sizeof register_cache] loop
		if (p.state == CACHE_SLOT_ADDRESS) and (p.symbol == sym) and (p.number == (off as Word)) then
			reg := p.reg;
			return;
		end if;
		p := @next p;
	end loop;
end sub;

sub RegCacheFindValue(sym: [Symbol], off: Size): (reg: RegId)
	reg := 0;
	var p := &register_cache[0];
	while p != &register_cache[@sizeof register_cache] loop
		if (p.state == CACHE_SLOT_VALUE) and (p.symbol == sym) and (p.number == (off as Word)) then
			reg := p.reg;
			return;
		end if;
		p := @next p;
	end loop;
end sub;

