include "cowgol.coh";
include "argv.coh";
include "strings.coh";
include "malloc.coh";
include "file.coh";

typedef string is [uint8];
typedef Size is uint16;

include "include/coodecls.coh";
include "src/cowlink/types.coh";
include "src/cowlink/utils.coh";
include "src/cowlink/emitter.coh";
include "archlink.coh";
include "src/cowlink/cooread.coh";
include "src/cowlink/streams.coh";
include "src/cowlink/asmwrite.coh";
include "src/cowlink/graph.coh";

print("COWLINK: ");
print_i16((GetFreeMemory() >> 10) as uint16);
print("kB free\n");

var coos: [Coo] := 0 as [Coo];
var firstCoo: [Coo] := 0 as [Coo];
var outputFilename: [uint8] := 0 as [uint8];
var mainSubroutine: [Subroutine] := 0 as [Subroutine];
var mostRecentSubroutine: [Subroutine] := 0 as [Subroutine];

sub SyntaxError() is
	SimpleError("Syntax error\n");
end sub;

sub AddInputFile(filename: [uint8]) is
	print("Adding input file: ");
	print(filename);
	print_nl();

	var coo := OpenAndLoadCoo(filename);
	if firstCoo == nil then
		firstCoo := coo;
		coos := coo;
	else
		coos.next := coo;
		coos := coo;
	end if;

	var thisMain := [FindSub(coo, 0)];
	if thisMain != nil then
		if mainSubroutine == nil then
			mainSubroutine := thisMain;
		end if;
	end if;
	if mostRecentSubroutine != nil then
		AddRef(mostRecentSubroutine, thisMain);
	end if;
	mostRecentSubroutine := thisMain;
end sub;

ArgvInit();
loop
	var arg := ArgvNext();
	if arg == nil then
		break;
	end if;

	if (StrICmp(arg, "-o") == 0) then
		outputFilename := ArgvNext();
	elseif [arg] == '-' then
		SyntaxError();
	else
		AddInputFile(arg);
	end if;
end loop;

if mainSubroutine == 0 as [Subroutine] then
	SimpleError("No main subroutine in any coofile");
end if;
if outputFilename == 0 as [uint8] then
	SimpleError("No output filename specified");
end if;

print("Analysing...\n");
ResolveExternals();
PlaceSubroutines(mainSubroutine);

EmitterOpenfile(outputFilename);

print("Creating output file: ");
print(outputFilename);
print_nl();
InitStreams();
SetStream(7);
ArchEmitHeader(firstCoo);
FlushStream();
WriteAllSubroutinesToOutputFile(firstCoo);
SetStream(7);
ArchEmitFooter(firstCoo);
FlushStream();

EmitterClosefile();

print("done: ");
print_i16((GetFreeMemory() >> 10) as uint16);
print("kB free\n");

