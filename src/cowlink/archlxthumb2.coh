const STACK_SIZE := 128;

var workspaceSize: Size[NUM_WORKSPACES];

sub E_nl() is
	E_b8('\n');
end sub;

sub ArchAlignUp(value: Size, alignment: uint8): (newvalue: Size) is
	newvalue := value;
end sub;

sub ArchEmitSubRef(subr: [Subroutine]) is
	E_b8('f');
	E_u16(subr.id);
	E_b8('_');
	E(subr.name);
end sub;

sub ArchEmitWSRef(wid: uint8, address: Size) is
	E("ws+");
	E_u16(address);
end sub;

sub ArchEmitHeader(coo: [Coo]) is
	E(".syntax unified\n.global _start\n.code 32\n");
	E(".global _start\n");
	E("\t_start:\n");
	E("\tblx _thumb_start\n");
	E(".code 16\n");
	E(".thumb_func\n");
	E("_thumb_start:\n");
	E("\tadd r1, sp, #4\n");
	E("\tldr r0, =argv\n");
	E("\tstr r1, [r0]\n");

	while coo != nil loop
		var main := coo.index.subroutines[0];
		if main != nil then
			E("\tbl ");
			ArchEmitSubRef(main);
			E_nl();
		end if;
		coo := coo.next;
	end loop;

	E("_exit:\n");
	E("\tmov r0, #0\n");
	E("\tmov r7, #1\n"); # __exit()
	E("\tsvc #0\n");
end sub;

sub ArchEmitFooter(coo: [Coo]) is
	E("\t.bss\n");
	E("argv: .space 4\n");
	E("ws: .space ");
	E_u16(workspaceSize[0]);
	E_nl();
	E("LOMEM: .space 1024*1024\n");
	E("HIMEM:\n");

	# This is ridiculous, but can't be helped: write spaces to pad the end
	# of the file. Unix C compilers don't like trailing nuls.

	var padding := FCBExt(&outfcb) - FCBPos(&outfcb);
	while padding != 0 loop
		E_b8(' ');
		padding := padding - 1;
	end loop;
end sub;


